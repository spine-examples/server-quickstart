/*
 * Copyright 2019, TeamDev. All rights reserved.
 *
 * Redistribution and use in source and/or binary forms, with or without
 * modification, must retain the above copyright notice and the following
 * disclaimer.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

plugins {
    id 'io.spine.tools.gradle.bootstrap' version '1.6.4' apply false
    id 'net.ltgt.errorprone' version '1.2.1' apply false
}

final def scripts = [
        version         : "$rootDir/version.gradle",
        tests           : "$rootDir/gradle/tests.gradle",
        jacocoCombined  : "$rootDir/gradle/jacoco-combined.gradle",
        staticAnalysis  : "$rootDir/gradle/static-analysis.gradle",
        idea            : "$rootDir/gradle/idea.gradle"
]

allprojects {
    apply from: "$scripts.version"
}

subprojects {
    project.ext {
        sourcesRootDir = "$projectDir/src"
        generatedRootDir = "$projectDir/generated"
    }
    apply from: "$scripts.idea"

    apply plugin: 'io.spine.tools.gradle.bootstrap'

    pluginManager.withPlugin('java') {
        apply plugin: 'net.ltgt.errorprone'
        apply plugin: 'jacoco'

        apply from: "$scripts.tests"
        apply from: "$scripts.staticAnalysis"

        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        dependencies {
            errorprone "com.google.errorprone:error_prone_core:$deps.versions.errorProne"
            errorproneJavac "com.google.errorprone:javac:$deps.versions.errorProneJavac"

            implementation "com.google.guava:guava:$deps.versions.guava"
            implementation "org.checkerframework:checker-qual:$deps.versions.checkerFramework"

            runtimeOnly "com.google.flogger:flogger:$deps.versions.flogger"
        }
    }
    spine {
        // Enforce the dependency versions needed by Spine.
        forceDependencies = true
    }
}

// Composes a combined coverage report on all Java-related modules in the project.
apply from: "$scripts.jacocoCombined"
